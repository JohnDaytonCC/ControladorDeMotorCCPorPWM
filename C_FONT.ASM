;======================================================
;PROGRAMA PARA CONTROLAR UM MOTOR CC POR PWM COM DOIS
;BOTÕES PARA AUMANTAR E DIMINUIR A VELOCIDADE DO MOTOR
;-------------------- PINAGEM -------------------------
; SAÍDA DOS PULSOS -> P1.0
; INTERRUPÇÃO EXT -> P3.1
; BOTÕES DE AUMENTO E DIMINUIÇÃO DE VELOCIDADE -> P2.0
; E P2.1
;------------------------------------------------------
; DESENVOLVIDO POR: JOHN DAYTON CHAVES CUTÓDIO
; DATA DE DESENVOLVIMENTO: 24/08/2013
; ULTIMA ATUALIZAÇÃO: 24/08/2013
;======================================================
;---------------- DEFINIÇÕES DAS FLAGS ----------------
FLAG	EQU	00H
MOTOR	EQU	P1.0
BOT_0	EQU	P2.0
BOT_1	EQU	P2.1
;------------- DESVIOS DE IMTERRUPÇÕES ----------------
	ORG	00H
	AJMP	INICIO
	ORG	03H
	AJMP	EXT_0
	ORG	0BH
	AJMP	TIMR_0
;================= INTERRUPÇÕES =======================
;------------------- EXTERNA 0 ------------------------
EXT_0:	JNB	BOT_0,MAIS	;ANALISA QUAL FOI O BOTÃO PRESSIONADO
				;SE FOR BOT_0 IRÁ SALTAR PARA (MAIS)

MENOS:	CJNE	R1,#50,SEG1	;VERIFICA SE R1 JÁ É MÁXIMO
	RETI			;SAI DA INTERRUPÇÃO SE O VALOR FOR MÁXIMO
SEG1:	INC	R1		;SE O VALOR NÃO FOR MÁXIMO INCREMENTA R1 E DECREMENTA R0
	DEC	R0
	MOV	A,R1
	MOV	R3,A		;REPASSA O VALOR DE R1 PARA R3
	RETI			;SAI DA INTERRUPÇÃO

MAIS:	CJNE	R0,#50,SEG2	;VERIFICA SE R0 JÁ É MÁXIMO
	RETI			;SAI DA INTERRUPÇÃO SE O VALOR FOR MÁXIMO
SEG2:	INC	R0		;SE O VALOR NÃO FOR MÁXIMO INCREMENTA R0 E DECREMENTA R1
	DEC	R1
	MOV	A,R0
	MOV	R2,A		;REPASSA O VALOR DE R0 PARA R2
	RETI			;;SAI DA INTERRUPÇÃO

;------------------ TIMER 0 ---------------------------
TIMR_0:	JB	FLAG,ROT_2	;VERIFICAÇÃO DE QUAL ESTADO ATUAL DO PINO MOTOR
			;ROTINA PARA TEMPO BAIXO
ROT_1:	CLR	MOTOR		;LIMPA VALOR DO PINO DO MOTOR
	CJNE	R3,#0,PART_1	;COMPARA R3 COM 0 SALTA PARA (PART_1) SE DIFERENTE
	RETI			;SAI DA INTERRUPÇÃO
PART_1:	DJNZ	R3,SAI_T0	;DECREMENTA R3 E DESVIA SE FOR DIFERENTE DE 0
	MOV	A,R1		
	MOV	R3,A		;CARREGA O VALOR DE R1 EM R3 NOVAMENTE
	CJNE	R3,#50,PART_5	;COMPARA VALOR DE R3 COM O MÁXIMO E NÃO ALTERA A FLAG SE IGUAL
	RETI
PART_5:	CPL	FLAG		;INVERTE O VALOR DA FLAG
	RETI
			;ROTINA PARA TEMPO ALTO
ROT_2:	SETB	MOTOR		;SETA VALOR DO PINO DO MOTOR
	CJNE	R2,#0,PART_2	;COMPARA R2 COM 0 E SALTA PARA (PART_2) SE DIFERENTE
	RETI			;SAI DA INTERRUPÇÃO
PART_2:	DJNZ	R2,SAI_T0	;DECREMENTA R2 E DESVIA SE FOR DIFERENTE DE 0
	MOV	A,R0
	MOV	R2,A		;CARREGA O VALOR DE R0 EM R2
	CJNE	R2,#50,PART_4	;COMPARA VALOR DE R2 COM O MÁXIMO E NÃO ALTERA A FLAG SE IGUAL
	RETI
PART_4:	CPL	FLAG		;INVERTE O VALOR DA FLAG
SAI_T0:	RETI			;SAI DA INTERRUPÇÃO
;---------------- ROTINA PRINCIPAL --------------------
INICIO:	MOV	SP,#2FH		;DESLOCAMENTO DA PILHA
	MOV	IE,#83H		;HABILITAÇÃO DAS INTERRUPÇÕES
				;HABILITA O TIMER0 O EXT_0 E A HABILITAÇÃO GERAL
	MOV	IP,#02H		;PRIORIDADES DAS INTERRUPÇÕES
				;PRIORIDADE MAIOR PARA TIMER0
	MOV	TMOD,#02H	;TIMR_0 EM MODO 2
	MOV	TH0,#0ECH	;CHAMADA A CADA 20us
	MOV	TL0,#0ECH	;PARA FREQUENCIA DE 1KHZ
	SETB	TR0		;LIGA O TIMER0
	SETB	IT0		;HABILITA O TIMER0 PARA BORDA DE DESCIDA
	MOV	R0,#0		;AUXILIAR DE R2 PARA AUMENTAR O VALOR DE TEMPO LIGADOO
	MOV	R2,#0
	MOV	R1,#50		;AUXILIAR DE R3 PARA AUMENTAR O VALOR DE TEMPO DESLIGADO 
	MOV	R3,#50
	CLR	FLAG
	AJMP	$		;AGUARDA AS INTERRUPÇÕES
;------------------- FIM DO PROGRAMA ------------------
	END